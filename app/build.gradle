/*
 * Copyright (C) by MinterTeam. 2018
 * @link <a href="https://github.com/MinterTeam">Org Github</a>
 * @link <a href="https://github.com/edwardstock">Maintainer Github</a>
 *
 * The MIT License
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

plugins {
    id 'com.android.application'
    id 'io.fabric'
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "network.minter.bipwallet"
        minSdkVersion minterMinSdk
        targetSdkVersion minterMaxSdk
        versionCode 40
        versionName "1.1.2"
        testInstrumentationRunner "network.minter.bipwallet.internal.WalletTestRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    lintOptions {
        disable 'CheckResult', 'DefaultLocale'
        abortOnError false
        ignoreWarnings true
    }

    aaptOptions {
        additionalParameters "--no-version-vectors"
    }


    signingConfigs {
        config {
            keyAlias getProperty("minter_key_alias")
            keyPassword getProperty("minter_key_password")
            storeFile file(getProperty("minter_key_store_file"))
            storePassword getProperty("minter_key_store_password")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.config
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-test-rules.pro'
        }

        debug {
            signingConfig signingConfigs.config
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-test-rules.pro', 'proguard-release-rules.pro'
            testProguardFile 'proguard-test-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "env"

    productFlavors {
        netMain {
            dimension "env"
        }

        netMainNoCrashlytics {
            dimension "env"
        }

        netTest {
            dimension "env"
        }

        netTestNoCrashlytics {
            dimension "env"
        }
    }
}

ext {
    minterExplorerSDK = "0.2.4"
    minterProfileSDK = "0.2.0"
    minterBlockchainSDK = "0.3.0"
    minterCoreSDK = "0.2.0"
}

dependencies {
    netTestImplementation([
            "network.minter.android:minter-android-core-testnet:${minterCoreSDK}",
            "network.minter.android:minter-android-explorer-testnet:${minterExplorerSDK}",
            "network.minter.android:minter-android-blockchain-testnet:${minterBlockchainSDK}",
            "network.minter.android:minter-android-profile-testnet:${minterProfileSDK}"
    ])
    netMainImplementation([
            "network.minter.android:minter-android-core:${minterCoreSDK}",
            "network.minter.android:minter-android-explorer:${minterExplorerSDK}",
            "network.minter.android:minter-android-blockchain:${minterBlockchainSDK}",
            "network.minter.android:minter-android-profile:${minterProfileSDK}"
    ])
    netTestNoCrashlyticsImplementation([
            "network.minter.android:minter-android-core-testnet:${minterCoreSDK}",
            "network.minter.android:minter-android-explorer-testnet:${minterExplorerSDK}",
            "network.minter.android:minter-android-blockchain-testnet:${minterBlockchainSDK}",
            "network.minter.android:minter-android-profile-testnet:${minterProfileSDK}"
    ])
    netMainNoCrashlyticsImplementation([
            "network.minter.android:minter-android-core:${minterCoreSDK}",
            "network.minter.android:minter-android-explorer:${minterExplorerSDK}",
            "network.minter.android:minter-android-blockchain:${minterBlockchainSDK}",
            "network.minter.android:minter-android-profile:${minterProfileSDK}"
    ])

    // support
    implementation "com.android.support:appcompat-v7:${minterLibSupport}"
    implementation "com.android.support:support-annotations:${minterLibSupport}"
    implementation "com.android.support:recyclerview-v7:${minterLibSupport}"
    implementation "com.android.support:cardview-v7:${minterLibSupport}"
    implementation "com.android.support:support-v13:${minterLibSupport}"
    implementation "com.android.support:design:${minterLibSupport}"
    implementation "com.android.support:support-vector-drawable:${minterLibSupport}"
    implementation "com.android.support:exifinterface:${minterLibSupport}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // android arch
    implementation 'android.arch.paging:runtime:1.0.1'
    implementation 'android.arch.paging:rxjava2:1.0.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }

    // yandex
    implementation 'com.yandex.android:mobmetricalib:3.1.0'
    implementation 'com.android.installreferrer:installreferrer:1.0'

    // common
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.budiyev.android:code-scanner:1.9.0'
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.google.zxing:android-core:3.3.0'
    implementation 'com.google.zxing:android-integration:3.3.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'org.parceler:parceler-api:1.1.11'
    annotationProcessor 'org.parceler:parceler:1.1.11'
    implementation 'com.orhanobut:hawk:2.0.1'
    implementation 'com.annimon:stream:1.2.0'
    implementation 'net.danlew:android.joda:2.9.9.1'
    implementation('com.fatboyindustrial.gson-jodatime-serialisers:gson-jodatime-serialisers:1.6.0') {
        exclude group: 'joda-time'
        exclude group: 'com.google.code.gson'
    }
    implementation('com.github.hotchemi:permissionsdispatcher:3.3.1') {
        // if you don't use android.app.Fragment you can exclude support for them
        exclude module: "support-v13"
    }
    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:3.3.1"
    implementation 'network.minter.android:centrifuge-android:0.38'

    // Annotations and DI
    annotationProcessor 'com.google.dagger:dagger-compiler:2.16'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.16'
    implementation 'com.google.dagger:dagger:2.16'
    implementation 'com.google.dagger:dagger-android-support:2.16'
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // MVP ignore 1.5.5 - not working with inner class interfaces, waiting for 1.5.6

    //noinspection GradleDependency
    implementation 'com.arello-mobile:moxy:1.5.3'

    //noinspection GradleDependency
    implementation 'com.arello-mobile:moxy-android:1.5.3'

    //noinspection GradleDependency
    implementation 'com.arello-mobile:moxy-app-compat:1.5.3'

    //noinspection GradleDependency
    annotationProcessor 'com.arello-mobile:moxy-compiler:1.5.3'

    // Reactive
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // networking
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    //ui
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.squareup.picasso:picasso:3.0.0-SNAPSHOT'
    implementation 'com.github.ittianyu:BottomNavigationViewEx:2.0.2'
    implementation 'com.makeramen:roundedimageview:2.3.0'

    // don't update to 2.2.0 - some issues when using elevation

    //noinspection GradleDependency
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    annotationProcessor 'io.sweers.barber:barber-compiler:1.3.1'
    implementation 'io.sweers.barber:barber-api:1.3.1'

    //noinspection GradleDynamicVersion,GradleDependency
    implementation 'network.minter.android:android-image-cropper:2.7.1'
    implementation 'com.redmadrobot:inputmask:3.4.4'
    implementation 'com.pnikosis:materialish-progress:1.7'

    // testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation 'com.squareup.rx.idler:rx2-idler:0.9.0'
}
repositories {
    mavenCentral()
}
